10 REM ** SPRITE MOVER **
20 REM **
29 REM **
30 REM ** VALUES
31 REM **
40 WH=255        : REM WHITE
50 CS=147        : REM CLEAR SCREEN CHAR
60 X=100         : REM SPRITE X
70 Y=100         : REM SPRITE Y
71 W=9           : REM W KEY
72 S=13          : REM S KEY
73 A=10          : REM A KEY
74 D=18          : REM D KEY
75 NO=64         : REM NO KEY PRESSED
76 KP=64         : REM LATEST KEY PRESS
79 REM **
80 REM ** SOUND LOCATIONS
81 REM **
90 MV=54296      : REM MASTER VOLUME
95 SG=54276      : REM SOUND GATE
100 AD=54277     : REM ATTACK/DELAY
110 SR=54278     : REM SUSTAIN/RELEASE
120 FL=54272     : REM FREQUENCY LOW
130 FH=54273     : REM FREQUENCY HIGH
139 REM **
140 REM ** SPRITE LOCATIONS
141 REM **
150 SE=53269     : REM SPRITE ENABLE
160 SX=53248     : REM SPRITE X
170 SY=53249     : REM SPRITE Y
180 SL=2040      : REM SPRITE 0
190 BL=13        : REM BLOCK 13
199 REM **
200 REM ** OTHER LOCATIONS
201 REM **
210 KB=197       : REM READ KEYBOARD KEY
299 REM **
300 REM ** START PROGRAM
301 REM **
310 PRINT CHR$(CS)  : REM CLR SCREEN
320 GOSUB 1000      : REM SET UP SPRITE
330 GOSUB 2000      : REM SET UP SOUND
340 GOSUB 4000      : REM READ KEY PRESS
350 GOSUB 5000      : REM MOVE SPRITE
360 GOSUB 3000      : REM PLAY SOUND
370 GOTO 340
990 END
999 REM **
1000 REM ** SET UP SPRITE
1001 REM **
1010 FOR I = (BL*64) TO (BL*64)+62
1020 POKE I, WH
1030 NEXT
1040 POKE SX, X
1050 POKE SY, Y
1060 POKE SE, 1  : REM ENABLE SPRITE
1070 RETURN
1999 REM **
2000 REM ** SET UP SOUND
2001 REM **
2010 POKE MV, 15    : REM MAX VOLUME
2020 POKE AD, 0     : REM ATTACK/DELAY
2030 POKE SR, 240   : REM SUST/REL
2040 POKE FL, 0     : REM FREQ LOW
2050 POKE FH, 20    : REM FREQ HIGH
2060 RETURN
2999 REM **
3000 REM ** PLAY SOUND
3001 REM **
3010 POKE SG, 17  : REM SOUND TRIANGLE
3020 FOR I=1 TO 100: NEXT : REM WAIT
3030 POKE SG, 16  : REM STOP SOUND
3040 RETURN
3999 REM **
4000 REM ** READ KEYBOARD
4001 REM **
4010 KP = PEEK(KB) : REM READ KEY PRESS
4020 IF KP=W THEN Y=Y-5 : REM UP
4030 IF KP=S THEN Y=Y+5 : REM DOWN
4040 IF KP=A THEN X=X-5 : REM LEFT
4050 IF KP=D THEN X=X+5 : REM RIGHT
4060 IF KP=NO THEN GOTO 4010
4070 RETURN
4999 REM **
5000 REM ** RENDER SPRITE
5001 REM **
5010 REM ** KEEP SPRITE ON SCREEN
5020 IF Y < 50 THEN Y=50
5030 IF Y > 229 THEN Y=229
5040 IF X < 24 THEN X=24
5050 IF X > 255 THEN X=255
5060 POKE SX, X
5070 POKE SY, Y
5080 RETURN
